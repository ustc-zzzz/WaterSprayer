buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.+', changing: true
        classpath group: 'org.parchmentmc', name: 'librarian', version: '1.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

apply plugin: 'scala'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = '1.1.0'
group = 'com.github.ustc-zzzz'
archivesBaseName = project.name

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    // mappings channel: 'official', version: '1.16.5'
    mappings channel: 'parchment', version: '2021.10.17-1.16.5'
    runs {
        client {
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run')
            mods {
                watersprayer {
                    source sourceSets.main
                }
            }
        }
        server {
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP,CLASSLOADING'
            properties 'forge.logging.console.level': 'debug,trace'
            workingDirectory project.file('run')
            mods {
                watersprayer {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        name = 'Azure-SLP'
        url = 'https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1'
        content {
            includeModule 'org.typelevel', 'cats-core_2.13'
            includeModule 'org.typelevel', 'cats-kernel_2.13'
            includeModule 'com.kotori316', 'scalablecatsforce'
            excludeVersionByRegex '.*', '.*', '.*_mapped_snapshot_.*'
        }
    }
    mavenCentral()
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.20'

    implementation 'org.typelevel:cats-core_2.13:2.7.0'
    implementation 'org.scala-lang:scala-library:2.13.6'
    implementation 'com.kotori316:scalablecatsforce:2.13.5-build-2:dev'
}

jar {
    manifest.attributes([
        'Specification-Title'     : 'watersprayer',
        'Specification-Vendor'    : 'watersprayersareus',
        'Specification-Version'   : '1', // We are version 1 of ourselves
        'Implementation-Title'    : project.name,
        'Implementation-Version'  : project.version,
        'Implementation-Vendor'   : 'watersprayersareus',
        'Implementation-Timestamp': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ')
    ])
}
